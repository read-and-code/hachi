buildscript {
    ext {
        kotlinVersion = '1.3.61'
        jvmTarget = '1.8'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    group 'hachi'
    version '1.0-SNAPSHOT'

    repositories {
        jcenter()
    }
}

subprojects {
    if (it.name != 'hachi-antlr') {
        apply plugin: 'kotlin'

        configurations {
            ktlint
        }

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
            ktlint "com.github.shyiko:ktlint:0.29.0"
        }

        compileKotlin {
            kotlinOptions.jvmTarget = jvmTarget
        }

        compileTestKotlin {
            kotlinOptions.jvmTarget = jvmTarget
        }

        task ktlint(type: JavaExec, group: "verification") {
            description = "Check Kotlin code style."
            classpath = configurations.ktlint
            main = "com.github.shyiko.ktlint.Main"
            args "src/**/*.kt"
        }
    }
}
